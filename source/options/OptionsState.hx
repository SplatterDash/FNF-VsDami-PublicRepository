package options;

import states.MainMenuState;
import backend.StageData;
import backend.ClientPrefs;

class OptionsState extends MusicBeatState
{
	var options:Array<String> = [
		'Note Colors',
		'Controls',
		'Adjust Delay and Combo',
		'Graphics',
		'Visuals',
		'Gameplay',
		#if TRANSLATIONS_ALLOWED 'Language', #end
		'Erase Save Data'
	];
	private var grpOptions:FlxTypedGroup<Alphabet>;
	private static var curSelected:Int = 0;
	public static var menuBG:FlxSprite;
	public static var onPlayState:Bool = false;
	public var allowMouse:Bool = ClientPrefs.data.mouseUI;

	function openSelectedSubstate(label:String) {
		switch(label)
		{
			case 'Note Colors':
				openSubState(new options.NotesColorSubState());
			case 'Controls':
				openSubState(new options.ControlsSubState());
			case 'Graphics':
				openSubState(new options.GraphicsSettingsSubState());
			case 'Visuals':
				openSubState(new options.VisualsSettingsSubState());
			case 'Gameplay':
				openSubState(new options.GameplaySettingsSubState());
			case 'Adjust Delay and Combo':
				MusicBeatState.switchState(new options.NoteOffsetState());
			case 'Language':
				openSubState(new options.LanguageSubState());
			case 'Erase Save Data':
				openSubState(new options.EraseSaveSubState());
		}
	}

	var selectorLeft:Alphabet;
	var selectorRight:Alphabet;

	override function create()
	{
		#if DISCORD_ALLOWED
		DiscordClient.changePresence("Options Menu", null);
		#end

		if(FlxG.sound.music == null || !FlxG.sound.music.playing) FlxG.sound.playMusic(Paths.music('options'), 1);

		var bg:FlxSprite = new FlxSprite().loadGraphic(Paths.image('menubackgrounds/backgroundDesat'));
		bg.antialiasing = ClientPrefs.data.antialiasing;
		bg.color = 0xFFea71fd;
		bg.updateHitbox();

		bg.screenCenter();
		add(bg);

		grpOptions = new FlxTypedGroup<Alphabet>();
		add(grpOptions);

		for (num => option in options)
		{
			var optionText:Alphabet = new Alphabet(0, 0, Language.getPhrase('options_$option', option), true);
			optionText.screenCenter();
			optionText.y += (92 * (num - (options.length / 2))) + 45;
			grpOptions.add(optionText);
		}

		selectorLeft = new Alphabet(0, 0, '>', true);
		add(selectorLeft);
		selectorRight = new Alphabet(0, 0, '<', true);
		add(selectorRight);

		changeSelection();
		ClientPrefs.saveSettings();

		super.create();
	}

	override function closeSubState()
	{
		super.closeSubState();
		ClientPrefs.saveSettings();
		#if DISCORD_ALLOWED
		DiscordClient.changePresence("Options Menu", null);
		#end
	}

	var timeNotMoving:Float = 0;
	override function update(elapsed:Float) {
		super.update(elapsed);

		if (controls.UI_UP_P || (allowMouse && FlxG.mouse.wheel > 0))
			changeSelection(-1);
		if (controls.UI_DOWN_P || (allowMouse && FlxG.mouse.wheel < 0))
			changeSelection(1);

		if (allowMouse && (FlxG.mouse.deltaScreenX != 0 && FlxG.mouse.deltaScreenY != 0)) //FlxG.mouse.deltaScreenX/Y checks is more accurate than FlxG.mouse.justMoved
		{
			//allowMouse = false;
			FlxG.mouse.visible = true;
			timeNotMoving = 0;

			for(item in grpOptions) {
				if(FlxG.mouse.overlaps(item) && grpOptions.members.indexOf(item) != curSelected && !FlxG.mouse.overlaps(grpOptions.members[curSelected])) changeSelection(grpOptions.members.indexOf(item), true);
				continue;
			}
		}
		else
		{
			timeNotMoving += elapsed;
			if(timeNotMoving > 2) FlxG.mouse.visible = false;
		}

		if (controls.BACK)
		{
			FlxG.sound.play(Paths.sound('cancelMenu'));
			if(onPlayState)
			{
				StageData.loadDirectory(PlayState.SONG);
				LoadingState.loadAndSwitchState(new PlayState());
				FlxG.sound.music.volume = 0;
			}
			else {
				MusicBeatState.switchState(new MainMenuState());
				FlxG.sound.playMusic(Paths.music('menu_v1'), 0.7);
					FlxG.sound.music.loopTime = 8727;
			}
		}
		else if (controls.ACCEPT || (allowMouse && FlxG.mouse.justPressed && FlxG.mouse.overlaps(grpOptions.members[curSelected]))) openSelectedSubstate(options[curSelected]);
	}
	
	function changeSelection(change:Int = 0, ?force:Bool = false)
	{
		curSelected = FlxMath.wrap(force ? change : curSelected + change, 0, options.length - 1);

		for (num => item in grpOptions.members)
		{
			item.targetY = num - curSelected;
			item.alpha = 0.6;
			if (item.targetY == 0)
			{
				item.alpha = 1;
				selectorLeft.x = item.x - 63;
				selectorLeft.y = item.y;
				selectorRight.x = item.x + item.width + 15;
				selectorRight.y = item.y;
			}
		}
		FlxG.sound.play(Paths.sound('scrollMenu'));
	}

	override function destroy()
	{
		ClientPrefs.loadPrefs();
		super.destroy();
	}
}